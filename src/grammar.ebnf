/** js sequence diagrams
 *  http://bramp.github.io/js-sequence-diagrams/
 *  (c) 2012-2013 Andrew Brampton (bramp.net)
 *  Simplified BSD license.
 * 
 * EBNF version of the grammar for diagraming purposes only
 * Paste this into http://www.bottlecaps.de/rr/ui to generate the diagram 
 */
document ::= statement*

statement ::=
	( 'title' ':' message (attributes)? 
	| 'participant' actor ('as' alias)? (attributes)?
	| 'note' (
		( 'left of' | 'right of') actor
		| 'over' (actor | actor ',' actor)
		) ':' message (attributes)?
	| actor ( '-' | '--' ) ( '>' | '>>' )? actor ':' message (attributes)?
	)

attributes ::=  '[' attribute (',' attribute)* ']'

attribute ::= key '=' '"' value '"'

/*
message ::= [^\n]+

actor ::= [^\->:\n,]+
*/
